plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version "${forge_gradle_version}"
    id 'org.spongepowered.mixin' version "${mixin_gradle_version}"
    id 'wtf.gofancy.fancygradle' version "${fancy_gradle_version}"
    id 'com.github.johnrengelman.shadow' version "${shadow_version}"
}

version = mod_version
group = "yeelp.${mod_id}"
base.archivesName = mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            args "--username=InDev"

            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {

            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {
    flatDir { dirs 'libs' }
    maven { url = 'https://www.cursemaven.com' }
    mavenCentral()
    maven {
        url "https://www.ryanliptak.com/maven/"
    }
    maven {
        name = "CraftTweaker/ContentTweaker"
        url = "https://maven.blamejared.com"
    }
    maven {
        name 'DVS1 Maven FS'
        url 'https://dvs1.progwml6.com/files/maven'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("applecore:AppleCore:1.12.2-3.4.0+356.64453:api")
    compileOnly fg.deobf("curse.maven:spiceoflife-220811:2571951")
    compileOnly fg.deobf("curse.maven:spiceoflifecarrotedition-277616:2731062")
    compileOnly fg.deobf("curse.maven:conarm-287683:3174535")
    compileOnly fg.deobf("curse.maven:appleskin-248787:2987247")
    compileOnly fg.deobf("curse.maven:lemonskin-1337144:7397823")
    compileOnly fg.deobf("slimeknights.mantle:Mantle:1.12-1.3.3.55")
    compileOnly fg.deobf("slimeknights:TConstruct:1.12.2-2.13.0.183")
    compileOnly fg.deobf("CraftTweaker2:CraftTweaker2-API:4.1.20.674")
    compileOnly fg.deobf("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.674")
    compileOnly fg.deobf("CraftTweaker2:ZenScript:4.1.20.674")
    compileOnly fg.deobf("curse.maven:fermiumbooter-968474:5672830")

    shadow 'io.github.llamalad7:mixinextras-common:0.3.5'

    shadow 'org.spongepowered:mixin:0.8.7'
    annotationProcessor 'org.spongepowered:mixin:0.8.7:processor'
}

mixin {
    add sourceSets.main, "mixins.${mod_id}.refmap.json"
}

fancyGradle {
    patches {
        resources
        coremods
        asm
        mergetool
    }
}

import org.apache.tools.ant.filters.ReplaceTokens;

processResources {
    var replaceProperties = [
            'mod_id': mod_id,
            'mod_name': mod_name,
            'mod_version': mod_version,
            'minecraft_version': minecraft_version,
            'mod_description': mod_description,
            'mod_author': mod_author
    ]

    inputs.properties replaceProperties

    filesMatching('mcmod.info') {
        expand replaceProperties
    }
}

task processSources(type: Sync) {
    from sourceSets.main.java
    inputs.property 'version', project.version
    filter(ReplaceTokens, tokens:[version: version.toString()])
    into 'build/src'
}

jar {
    manifest {
        attributes([
                'Specification-Title'        : mod_name,
                'Specification-Vendor'       : mod_author,
                'Specification-Version'      : '1',
                'Implementation-Title'       : mod_name,
                'Implementation-Vendor'      : mod_author,
                'Implementation-Version'     : mod_version,
                'Implementation-Timestamp'   : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "FMLCorePlugin"              : "${mixin_loader_class}",
                "FMLCorePluginContainsFMLMod": "true",
                "ForceLoadAsMod"             : "true",
                "TweakClass"                 : "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder"                 : 0,
        ])
    }
    archiveClassifier.set('THIN')
}

compileJava {
    source = processSources.outputs
}

java {
    withSourcesJar()
}

shadowJar {
    configurations = [project.configurations.shadow]
    mergeServiceFiles()

    exclude 'org/spongepowered/asm/launch/MixinLaunchPlugin*'
    exclude 'org/spongepowered/asm/launch/MixinTransformationService*'
    exclude 'org/spongepowered/asm/launch/platform/container/ContainerHandleModLauncherEx*'

    exclude 'LICENSE.txt'

    archiveClassifier.set('')
}

reobf {
    shadowJar{}
}

task signJars(dependsOn: reobfShadowJar) {
    doLast {
        signJar(jar.archivePath)
        signJar(shadowJar.archivePath)
    }
}

def signJar(path) {
    def alias = project.property("$alias")
    def keyStore = project.property("$keyStore")
    def storePass = project.property("$storePass")
    def exec_line = "jarsigner -keystore " + "$keyStore" + " -storepass " + "$storePass" + " " + path + " " + "$alias"
    exec_line.execute()
}

build.dependsOn(shadowJar)
jar.finalizedBy('reobfJar')
reobfShadowJar.doLast {
    signJar(shadowJar.archivePath)
}