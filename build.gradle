plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version "${forge_gradle_version}"
}

version = mod_version
group = "yeelp.${mod_id}"
base.archivesName = mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            args "--username=InDev"

            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {

            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {
    flatDir { dirs 'libs' }
    maven { url = 'https://www.cursemaven.com' }
    mavenCentral()
    maven {
        url "https://www.ryanliptak.com/maven/"
    }
    maven {
        name = "CraftTweaker/ContentTweaker"
        url = "https://maven.blamejared.com"
    }
    maven {
        name 'DVS1 Maven FS'
        url 'https://dvs1.progwml6.com/files/maven'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("applecore:AppleCore:1.12.2-3.4.0+356.64453:api")
    implementation fg.deobf("curse.maven:spiceoflife-220811:2571951")
    implementation fg.deobf("curse.maven:spiceoflifecarrotedition-277616:2731062")
    implementation fg.deobf("curse.maven:conarm-287683:3174535")
    implementation fg.deobf("slimeknights.mantle:Mantle:1.12-1.3.3.55")
    implementation fg.deobf("slimeknights:TConstruct:1.12.2-2.13.0.183")
    implementation fg.deobf("CraftTweaker2:CraftTweaker2-API:4.1.20.674")
    implementation fg.deobf("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.674")
    implementation fg.deobf("CraftTweaker2:ZenScript:4.1.20.674")
}

import org.apache.tools.ant.filters.ReplaceTokens;

processResources {
    var replaceProperties = [
            'mod_id': mod_id,
            'mod_name': mod_name,
            'mod_version': mod_version,
            'minecraft_version': minecraft_version,
            'mod_description': mod_description,
            'mod_author': mod_author
    ]

    inputs.properties replaceProperties

    filesMatching('mcmod.info') {
        expand replaceProperties
    }
}

task processSources(type: Sync) {
    from sourceSets.main.java
    inputs.property 'version', project.version
    filter(ReplaceTokens, tokens:[version: version.toString()])
    into 'build/src'
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : '1',
                'Implementation-Title'    : mod_name,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Version'  : mod_version,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

compileJava {
    source = processSources.outputs
}

java {
    withSourcesJar()
}

jar.finalizedBy('reobfJar')